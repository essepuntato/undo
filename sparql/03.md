# Iteration 3: formal competency questions


## Question 1

In which step of all the workflows the first version of a document has been produced?

    PREFIX : <https://w3id.org/un/data/undo/>
    PREFIX frbr: <http://purl.org/vocab/frbr/core#>
    PREFIX pwo: <http://purl.org/spar/pwo/>
    PREFIX undo: <https://w3id.org/un/ontology/undo/>
    
    SELECT ?doc ?step 
    WHERE {
        ?step a undo:Step ;
            pwo:produces ?doc .
            ?doc a undo:DocumentVersion .
        FILTER NOT EXISTS {
            ?another_step a undo:Step ;
                pwo:produces ?prev_version .
            ?prev_version frbr:revision+ ?doc
        }
    }


## Question 2

How many steps (on average) are used in each workflow?

    PREFIX : <https://w3id.org/un/data/undo/>
    PREFIX undo: <https://w3id.org/un/ontology/undo/>
    PREFIX owl: <http://www.w3.org/2002/07/owl#>
    PREFIX pwo: <http://purl.org/spar/pwo/>
    
    SELECT (avg(?n_step) as ?average)
    WHERE {
        {
            SELECT ?workflow (count(?step) as ?n_step)
            {
                ?workflow a undo:Workflow ;
                    pwo:hasStep ?step .
            } GROUP BY ?workflow
        }
    } GROUP BY ?workflow


## Question 3

How much did it take to executing each step in each workflow execution?

    PREFIX : <https://w3id.org/un/data/undo/>
    PREFIX undo: <https://w3id.org/un/ontology/undo/>
    PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
    PREFIX pwo: <http://purl.org/spar/pwo/>
    PREFIX taskex: <http://www.ontologydesignpatterns.org/cp/owl/taskexecution.owl#>
    
    SELECT ?execution ?step (round(?end - ?start) as ?minutes)
    WHERE {
        {
            SELECT ?execution ?step (min(?s_date) as ?start) (max(?e_date) as ?end) {
                ?execution a undo:WorkflowExecution ;
                    pwo:involvesAction ?action .
                ?action a undo:Action ;
                    taskex:executesTask ?step ;
                    pwo:happened ?time .
                ?time a undo:Interval ;
                    ti:hasIntervalStartDate ?s_date ;
                    ti:hasIntervalEndDate ?e_date .
            } GROUP BY ?execution ?step
        }
    }


## Question 4

What was the last entities produced by all the workflow executions?

    PREFIX : <https://w3id.org/un/data/undo/>
    PREFIX undo: <https://w3id.org/un/ontology/undo/>
    PREFIX pwo: <http://purl.org/spar/pwo/>
    PREFIX taskex: <http://www.ontologydesignpatterns.org/cp/owl/taskexecution.owl#>
    
    SELECT ?execution ?entity {
        ?execution a undo:WorkflowExecution ;
            pwo:involvesAction/taskex:executesTask ?step .
        ?step a undo:Step ;
            pwo:produces ?entity .
        FILTER NOT EXISTS {
            ?step pwo:hasNextStep ?another_step .
        }
    }